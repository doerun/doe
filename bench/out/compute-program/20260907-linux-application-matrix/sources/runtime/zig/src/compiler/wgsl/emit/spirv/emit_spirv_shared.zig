const std = @import("std");

const ir = @import("../../ir/ir.zig");
const spirv = @import("spirv_builder.zig");

pub const EmitError = spirv.EmitError || error{
    InvalidIr,
};

pub fn emitUncachedResultInst(
    builder: *spirv.Builder,
    allocator: std.mem.Allocator,
    opcode: u16,
    result_type: u32,
    operands: []const u32,
) EmitError!u32 {
    const result_id = builder.reserve_id();
    var words = std.ArrayListUnmanaged(u32){};
    defer words.deinit(allocator);
    try words.append(allocator, result_type);
    try words.append(allocator, result_id);
    try words.appendSlice(allocator, operands);
    try builder.append_function_inst(opcode, words.items);
    return result_id;
}

pub fn addr_space_to_storage_class(addr_space: ir.AddressSpace) u32 {
    return switch (addr_space) {
        .function => spirv.StorageClass.Function,
        .private => spirv.StorageClass.Private,
        .workgroup => spirv.StorageClass.Workgroup,
        .uniform => spirv.StorageClass.Uniform,
        .storage => spirv.StorageClass.StorageBuffer,
        .handle => spirv.StorageClass.UniformConstant,
    };
}

pub fn builtin_to_spirv(builtin: ir.Builtin) EmitError!u32 {
    return switch (builtin) {
        .position => spirv.Builtin.Position,
        .vertex_index => spirv.Builtin.VertexIndex,
        .instance_index => spirv.Builtin.InstanceIndex,
        .frag_depth => spirv.Builtin.FragDepth,
        .front_facing => spirv.Builtin.FrontFacing,
        .sample_index => spirv.Builtin.SampleIndex,
        .sample_mask => spirv.Builtin.SampleMask,
        .global_invocation_id => spirv.Builtin.GlobalInvocationId,
        .local_invocation_id => spirv.Builtin.LocalInvocationId,
        .local_invocation_index => spirv.Builtin.LocalInvocationIndex,
        .workgroup_id => spirv.Builtin.WorkgroupId,
        .num_workgroups => spirv.Builtin.NumWorkgroups,
        .subgroup_size => spirv.Builtin.SubgroupSize,
        .subgroup_invocation_id => spirv.Builtin.SubgroupLocalInvocationId,
        .clip_distances => spirv.Builtin.ClipDistance,
        .primitive_index => spirv.Builtin.PrimitiveId,
        .none => error.InvalidIr,
    };
}

pub fn builtin_to_spirv_for_stage(
    builtin: ir.Builtin,
    stage: ir.ShaderStage,
    storage_class: u32,
) EmitError!u32 {
    if (builtin == .position and stage == .fragment and storage_class == spirv.StorageClass.Input) {
        return spirv.Builtin.FragCoord;
    }
    return builtin_to_spirv(builtin);
}

test "uncached result instruction reserves id and writes canonical operands" {
    const allocator = std.testing.allocator;
    var builder = spirv.Builder.init(allocator);
    defer builder.deinit();

    const result_id = try emitUncachedResultInst(
        &builder,
        allocator,
        spirv.Opcode.IAdd,
        9,
        &.{ 4, 5 },
    );
    try std.testing.expectEqual(@as(u32, 1), result_id);
    try std.testing.expectEqualSlices(
        u32,
        &.{ (@as(u32, 5) << 16) | spirv.Opcode.IAdd, 9, 1, 4, 5 },
        builder.functions.items,
    );
}
