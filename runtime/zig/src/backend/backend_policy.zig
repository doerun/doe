const std = @import("std");
const backend_ids = @import("backend_ids.zig");

pub const BackendLane = enum {
    metal_doe_app,
    metal_doe_directional,
    metal_doe_comparable,
    metal_doe_release,
    metal_dawn_release,
    metal_webkit_release,
    metal_webkit_comparable,
    vulkan_doe_app,
    vulkan_doe_comparable,
    vulkan_doe_release,
    vulkan_dawn_release,
    d3d12_doe_app,
    d3d12_doe_directional,
    d3d12_doe_comparable,
    d3d12_doe_release,
    d3d12_dawn_release,
};

pub const UploadPathPolicy = enum {
    allow_mapped_shortcuts,
    staged_copy_only,
};

pub const SelectionPolicy = struct {
    lane: BackendLane,
    default_backend: backend_ids.BackendId,
    allow_fallback: bool,
    strict_no_fallback: bool,
    policy_hash: []const u8,
    upload_path_policy: UploadPathPolicy,
};

pub const LoadedSelectionPolicy = struct {
    policy: SelectionPolicy,
    owned_policy_hash: []u8,
};

pub const DEFAULT_RUNTIME_POLICY_PATH = "config/backend-runtime-policy.json";
const MAX_RUNTIME_POLICY_BYTES: usize = 64 * 1024;
const MAX_RUNTIME_POLICY_SEARCH_DEPTH: usize = 4;
const EXPECTED_SCHEMA_VERSION: i64 = 2;
const DEFAULT_POLICY_HASH = "backend-runtime-policy-v3";

pub const PolicyLoadError = error{
    InvalidRuntimePolicy,
};

const LaneSpec = struct {
    lane: BackendLane,
    name: []const u8,
    aliases: []const []const u8 = &.{},
};

const LANE_SPECS = [_]LaneSpec{
    .{ .lane = .metal_doe_app, .name = "metal_doe_app" },
    .{ .lane = .metal_doe_directional, .name = "metal_doe_directional" },
    .{ .lane = .metal_doe_comparable, .name = "metal_doe_comparable" },
    .{ .lane = .metal_doe_release, .name = "metal_doe_release" },
    .{ .lane = .metal_dawn_release, .name = "metal_dawn_release" },
    .{ .lane = .metal_webkit_release, .name = "metal_webkit_release" },
    .{ .lane = .metal_webkit_comparable, .name = "metal_webkit_comparable" },
    .{ .lane = .vulkan_doe_app, .name = "vulkan_doe_app" },
    .{ .lane = .vulkan_doe_comparable, .name = "vulkan_doe_comparable" },
    .{ .lane = .vulkan_doe_release, .name = "vulkan_doe_release" },
    .{
        .lane = .vulkan_dawn_release,
        .name = "vulkan_dawn_release",
        .aliases = &.{"vulkan_dawn_directional"},
    },
    .{ .lane = .d3d12_doe_app, .name = "d3d12_doe_app" },
    .{ .lane = .d3d12_doe_directional, .name = "d3d12_doe_directional" },
    .{ .lane = .d3d12_doe_comparable, .name = "d3d12_doe_comparable" },
    .{ .lane = .d3d12_doe_release, .name = "d3d12_doe_release" },
    .{ .lane = .d3d12_dawn_release, .name = "d3d12_dawn_release" },
};

const MAX_LANE_NAME_BYTES = blk: {
    var max_len: usize = 0;
    for (LANE_SPECS) |spec| {
        if (spec.name.len > max_len) max_len = spec.name.len;
        for (spec.aliases) |alias| {
            if (alias.len > max_len) max_len = alias.len;
        }
    }
    break :blk max_len;
};

fn normalizedLaneToken(raw: []const u8, buffer: *[MAX_LANE_NAME_BYTES]u8) ?[]const u8 {
    if (raw.len == 0 or raw.len > buffer.len) return null;
    for (raw, 0..) |char, index| {
        const normalized_char = if (char == '-') '_' else std.ascii.toLower(char);
        buffer[index] = normalized_char;
    }
    return buffer[0..raw.len];
}

pub fn lane_name(lane: BackendLane) []const u8 {
    inline for (LANE_SPECS) |spec| {
        if (spec.lane == lane) return spec.name;
    }
    unreachable;
}

pub fn parse_lane(raw: []const u8) ?BackendLane {
    var normalized_buffer: [MAX_LANE_NAME_BYTES]u8 = undefined;
    const normalized = normalizedLaneToken(raw, &normalized_buffer) orelse return null;

    inline for (LANE_SPECS) |spec| {
        if (std.mem.eql(u8, normalized, spec.name)) return spec.lane;
        inline for (spec.aliases) |alias| {
            if (std.mem.eql(u8, normalized, alias)) return spec.lane;
        }
    }
    return null;
}

pub fn load_policy_for_lane(
    allocator: std.mem.Allocator,
    policy_path: []const u8,
    lane: BackendLane,
) !LoadedSelectionPolicy {
    const bytes = try read_policy_file_alloc(allocator, policy_path);
    defer allocator.free(bytes);

    var arena = std.heap.ArenaAllocator.init(allocator);
    defer arena.deinit();

    const parsed = try std.json.parseFromSlice(std.json.Value, arena.allocator(), bytes, .{
        .ignore_unknown_fields = false,
    });
    defer parsed.deinit();

    const root_obj = switch (parsed.value) {
        .object => |obj| obj,
        else => return PolicyLoadError.InvalidRuntimePolicy,
    };

    const schema_value = root_obj.get("schemaVersion") orelse return PolicyLoadError.InvalidRuntimePolicy;
    const schema_version = switch (schema_value) {
        .integer => |value| value,
        else => return PolicyLoadError.InvalidRuntimePolicy,
    };
    if (schema_version != EXPECTED_SCHEMA_VERSION) {
        return PolicyLoadError.InvalidRuntimePolicy;
    }

    const policy_hash_value = root_obj.get("selectionPolicyHashSeed") orelse return PolicyLoadError.InvalidRuntimePolicy;
    const policy_hash = switch (policy_hash_value) {
        .string => |value| value,
        else => return PolicyLoadError.InvalidRuntimePolicy,
    };
    const owned_policy_hash = try allocator.dupe(u8, policy_hash);
    errdefer allocator.free(owned_policy_hash);

    const lanes_value = root_obj.get("lanes") orelse return PolicyLoadError.InvalidRuntimePolicy;
    const lanes_obj = switch (lanes_value) {
        .object => |obj| obj,
        else => return PolicyLoadError.InvalidRuntimePolicy,
    };

    const lane_value = lanes_obj.get(lane_name(lane)) orelse return PolicyLoadError.InvalidRuntimePolicy;
    const lane_obj = switch (lane_value) {
        .object => |obj| obj,
        else => return PolicyLoadError.InvalidRuntimePolicy,
    };

    const backend_value = lane_obj.get("defaultBackend") orelse return PolicyLoadError.InvalidRuntimePolicy;
    const backend_name = switch (backend_value) {
        .string => |value| value,
        else => return PolicyLoadError.InvalidRuntimePolicy,
    };
    const backend_id = backend_ids.parse_backend_id(backend_name) orelse return PolicyLoadError.InvalidRuntimePolicy;

    const allow_fallback_value = lane_obj.get("allowFallback") orelse return PolicyLoadError.InvalidRuntimePolicy;
    const allow_fallback = switch (allow_fallback_value) {
        .bool => |value| value,
        else => return PolicyLoadError.InvalidRuntimePolicy,
    };

    const strict_no_fallback_value = lane_obj.get("strictNoFallback") orelse return PolicyLoadError.InvalidRuntimePolicy;
    const strict_no_fallback = switch (strict_no_fallback_value) {
        .bool => |value| value,
        else => return PolicyLoadError.InvalidRuntimePolicy,
    };
    if (allow_fallback or !strict_no_fallback) {
        return PolicyLoadError.InvalidRuntimePolicy;
    }

    const upload_path_policy = blk: {
        const upload_path_policy_value = lane_obj.get("uploadPathPolicy") orelse break :blk UploadPathPolicy.allow_mapped_shortcuts;
        const upload_path_policy_name = switch (upload_path_policy_value) {
            .string => |value| value,
            else => return PolicyLoadError.InvalidRuntimePolicy,
        };
        break :blk parse_upload_path_policy(upload_path_policy_name) orelse return PolicyLoadError.InvalidRuntimePolicy;
    };
    if (strict_staged_upload_policy_required(lane) and upload_path_policy != .staged_copy_only) {
        return PolicyLoadError.InvalidRuntimePolicy;
    }

    return .{
        .policy = .{
            .lane = lane,
            .default_backend = backend_id,
            .allow_fallback = allow_fallback,
            .strict_no_fallback = strict_no_fallback,
            .policy_hash = owned_policy_hash,
            .upload_path_policy = upload_path_policy,
        },
        .owned_policy_hash = owned_policy_hash,
    };
}

fn read_policy_file_alloc(allocator: std.mem.Allocator, policy_path: []const u8) ![]u8 {
    var candidate_buffer: [std.fs.max_path_bytes]u8 = undefined;
    var depth: usize = 0;
    while (depth <= MAX_RUNTIME_POLICY_SEARCH_DEPTH) : (depth += 1) {
        const candidate = if (depth == 0) policy_path else blk: {
            const prefix_len = depth * "../".len;
            if (prefix_len + policy_path.len > candidate_buffer.len) return error.NameTooLong;
            var index: usize = 0;
            while (index < depth) : (index += 1) {
                const start = index * "../".len;
                @memcpy(candidate_buffer[start .. start + "../".len], "../");
            }
            @memcpy(candidate_buffer[prefix_len .. prefix_len + policy_path.len], policy_path);
            break :blk candidate_buffer[0 .. prefix_len + policy_path.len];
        };
        return std.fs.cwd().readFileAlloc(allocator, candidate, MAX_RUNTIME_POLICY_BYTES) catch |err| switch (err) {
            error.FileNotFound => continue,
            else => return err,
        };
    }
    return error.FileNotFound;
}

pub fn default_policy_for_lane(lane: BackendLane) SelectionPolicy {
    return switch (lane) {
        .metal_doe_app => .{
            .lane = lane,
            .default_backend = .doe_metal,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .allow_mapped_shortcuts,
        },
        .metal_doe_comparable, .metal_doe_release => .{
            .lane = lane,
            .default_backend = .doe_metal,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .staged_copy_only,
        },
        .metal_doe_directional => .{
            .lane = lane,
            .default_backend = .doe_metal,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .allow_mapped_shortcuts,
        },
        .metal_dawn_release => .{
            .lane = lane,
            .default_backend = .dawn_delegate,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .allow_mapped_shortcuts,
        },
        .metal_webkit_release => .{
            .lane = lane,
            .default_backend = .webkit_delegate,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .allow_mapped_shortcuts,
        },
        .metal_webkit_comparable => .{
            .lane = lane,
            .default_backend = .webkit_delegate,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .staged_copy_only,
        },
        .vulkan_doe_app => .{
            .lane = lane,
            .default_backend = .doe_vulkan,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .allow_mapped_shortcuts,
        },
        .vulkan_doe_comparable, .vulkan_doe_release => .{
            .lane = lane,
            .default_backend = .doe_vulkan,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .staged_copy_only,
        },
        .vulkan_dawn_release => .{
            .lane = lane,
            .default_backend = .dawn_delegate,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .allow_mapped_shortcuts,
        },
        .d3d12_doe_app => .{
            .lane = lane,
            .default_backend = .doe_d3d12,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .allow_mapped_shortcuts,
        },
        .d3d12_doe_comparable, .d3d12_doe_release => .{
            .lane = lane,
            .default_backend = .doe_d3d12,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .staged_copy_only,
        },
        .d3d12_doe_directional => .{
            .lane = lane,
            .default_backend = .doe_d3d12,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .allow_mapped_shortcuts,
        },
        .d3d12_dawn_release => .{
            .lane = lane,
            .default_backend = .dawn_delegate,
            .allow_fallback = false,
            .strict_no_fallback = true,
            .policy_hash = DEFAULT_POLICY_HASH,
            .upload_path_policy = .allow_mapped_shortcuts,
        },
    };
}

fn parse_upload_path_policy(raw: []const u8) ?UploadPathPolicy {
    if (std.mem.eql(u8, raw, "allow_mapped_shortcuts")) return .allow_mapped_shortcuts;
    if (std.mem.eql(u8, raw, "staged_copy_only")) return .staged_copy_only;
    return null;
}

fn strict_staged_upload_policy_required(lane: BackendLane) bool {
    return switch (lane) {
        .metal_doe_comparable,
        .metal_doe_release,
        .metal_webkit_comparable,
        .vulkan_doe_comparable,
        .vulkan_doe_release,
        .d3d12_doe_comparable,
        .d3d12_doe_release,
        => true,
        else => false,
    };
}

const testing = std.testing;

test "lane table round-trips canonical and alias names" {
    try testing.expectEqualStrings("metal_webkit_comparable", lane_name(.metal_webkit_comparable));
    try testing.expectEqual(@as(?BackendLane, .metal_webkit_comparable), parse_lane("metal_webkit_comparable"));
    try testing.expectEqual(@as(?BackendLane, .metal_webkit_comparable), parse_lane("metal-webkit-comparable"));
    try testing.expectEqual(@as(?BackendLane, .vulkan_dawn_release), parse_lane("vulkan_dawn_directional"));
}
